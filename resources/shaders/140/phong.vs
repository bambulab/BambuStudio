#version 140

const vec3 ZERO = vec3(0.0, 0.0, 0.0);

struct SlopeDetection
{
    bool actived;
    float normal_z;
    mat3 volume_world_normal_matrix;
};

uniform mat4 view_model_matrix;
uniform mat4 projection_matrix;
uniform mat3 normal_matrix;
uniform mat4 volume_world_matrix;
uniform SlopeDetection slope;

// Clipping plane, x = min z, y = max z. Used by the FFF and SLA previews to clip with a top / bottom plane.
uniform vec2 z_range;
// Clipping plane - general orientation. Used by the SLA gizmo.
uniform vec4 clipping_plane;
// Color clip plane - general orientation. Used by the cut gizmo.
uniform vec4 color_clip_plane;

in vec3 v_position;
in vec3 v_normal;

out vec3 clipping_planes_dots;
out float color_clip_plane_dot;

out vec4 world_pos;
out float world_normal_z;
out vec3 eye_normal;
out vec3 world_normal;
out vec3 eye_position;

void main()
{
    // First transform the normal into camera space and normalize the result.
    eye_normal = normalize(normal_matrix * v_normal);

    // World-space normal, used to anchor the diffuse key light to the scene
    // (light "from above") instead of following the camera.
    world_normal = mat3(volume_world_matrix) * v_normal;

    vec4 position = view_model_matrix * vec4(v_position, 1.0);
    eye_position = position.xyz;

    // Point in homogenous coordinates.
    world_pos = volume_world_matrix * vec4(v_position, 1.0);

    // z component of normal vector in world coordinate used for slope shading
    world_normal_z = slope.actived ? (normalize(slope.volume_world_normal_matrix * v_normal)).z : 0.0;

    gl_Position = projection_matrix * position;
    // Fill in the scalars for fragment shader clipping. Fragments with any of these components lower than zero are discarded.
    clipping_planes_dots = vec3(dot(world_pos, clipping_plane), world_pos.z - z_range.x, z_range.y - world_pos.z);
    color_clip_plane_dot = dot(world_pos, color_clip_plane);
}
